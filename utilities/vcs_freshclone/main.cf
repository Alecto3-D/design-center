bundle agent vcs_freshclone(prefix)
{
  vars:
      "canon_prefix" string => canonify("$(prefix)");
      "bundle_home"  string => dirname("$(this.promise_filename)"),
      policy => "overridable";

    use_git::
      # TODO: extend this to support multiple VCSs
      "vcs_update_command" slist =>
      {
        @(git_update_command)
      },
      policy => "overridable";

      "vcs_freshclone_command" slist =>
      {
        @(git_freshclone_command)
      },
      policy => "overridable";

      "vcs_configfile" string => "$(git_configfile)",
      policy => "overridable";

    any::
      "git_update_command" slist =>
      {
        # Remove untracked files
        "$($(prefix)vcs) clean -f -f -x -q -d .",
        # Discard changes to index and working tree since HEAD
        "$($(prefix)vcs) reset -q --hard HEAD",
        # Check out the proper branch
        "$($(prefix)vcs) checkout -q $($(prefix)branch)",
        # Pull the latest changes from origin
        "$($(prefix)vcs) pull -q -s recursive -Xtheirs origin $($(prefix)branch)",
        # Reset the working tree to the latest updates
        "$($(prefix)vcs) reset -q --hard HEAD",
      };

      "git_freshclone_command" slist =>
      {
        # clone the given repository with Git
        "$($(prefix)vcs) clone -b $($(prefix)branch) $($(prefix)origin) $($(prefix)path)"
      };

      "git_configfile" string => "$($(prefix)path)/.git/config";

  classes:
      # test if the last component of the "vcs" path arg matches a known VCS
      # TODO: use_git and known_vcs are global, so you can't mix VCSs in one run!!!
      "use_git" expression => strcmp(lastnode("$($(prefix)vcs)","/"), "git");
      "known_vcs" or => { "use_git" };
      "$(canon_prefix)path_exists" expression => fileexists("$($(prefix)path)");

      # I can't use vcs_configfile here because it doesn't converge soon enough
      use_git::
      "$(canon_prefix)vcsconfig_exists" expression => fileexists("$(git_configfile)");

  files:
    use_git::
      "$(git_configfile)"
      handle => "vcs_configfile_contents",
      comment => "Expand Git config file from template",
      create => "false",
      edit_defaults => empty,
      ifvarclass => "$(canon_prefix)vcsconfig_exists",
      edit_line => expand_template("$(bundle_home)/files/gitconfig.template");

  commands:

    known_vcs::
      # We only need to perform cleanup operations if we did not do a
      # fresh new clone
      "$(vcs_update_command)"
      handle     => "vcs_update_command",
      contain    => in_dir("$($(prefix)path)"),
      ifvarclass => "$(canon_prefix)vcsconfig_exists.!$(canon_prefix)vcs_freshcloned",
      comment    => "Update an existing VCS repository in $($(prefix)path)";

      "$(vcs_freshclone_command)"
      handle     => "vcs_update_command",
      ifvarclass => "!$(canon_prefix)vcsconfig_exists.!$(canon_prefix)vcs_freshcloned",
      classes    => if_ok("$(canon_prefix)vcs_freshcloned"),
      comment    => "Clone a VCS repository in $($(prefix)path)";

  reports:
    cfengine::
      "VCS is $($(prefix)vcs), origin $($(prefix)origin), branch $($(prefix)branch), target path $($(prefix)path)";

      "Cool, we're using Git."
      ifvarclass => "use_git";

    !known_vcs::
      "Sorry, the requested VCS $($(prefix)vcs) is not handled yet.";

    known_vcs::
      "$($(prefix)vcs) is known.  We will look for the config file $(vcs_configfile)";

      "VCS config file $(vcs_configfile) exists"
      ifvarclass => "$(canon_prefix)vcsconfig_exists.!$(canon_prefix)vcs_freshcloned";

      "VCS config file $(vcs_configfile) does not exist"
      ifvarclass => "!$(canon_prefix)vcsconfig_exists";

      "Sorry, we could not clone $($(prefix)origin) (branch $($(prefix)branch)) in $($(prefix)path) because that path already exists"
      ifvarclass => "$(canon_prefix)path_exists.!$(canon_prefix)vcsconfig_exists";

      "New clone of $($(prefix)origin) (branch $($(prefix)branch)) created in $($(prefix)path)"
      ifvarclass => "$(canon_prefix)vcs_freshcloned";

}

bundle agent meta_vcs_freshclone
{
  vars:
      # acceptable values: "/usr/bin/git" for example
      "optional_argument[vcs]" string => "string";

      "argument[path]"   string => "string";
      "argument[origin]" string => "string";
      "argument[branch]" string => "string";

      "default[vcs]"     string => "/usr/bin/git";

}
