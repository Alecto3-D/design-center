body file control
{
  namespace => "cfdc_db";
}

bundle agent db_install(class_prefix, db, process_match, start_command, packages, server_packages)
{
  meta:
      # __PREFIX__ will be supplied by cf-sketch
      "vars[class_prefix][type]"     string => "NON_EMPTY_STRING";
      "vars[class_prefix][default]"  string => "__CLASS_PREFIX__";

      "vars[db][type]"               string => "=mysql|=postgresql|=sqlite";

      # note that CONTEXT parameters are classes, not passed bundle parameters
      "vars[server][type]"           string => "CONTEXT";
      "vars[server][default]"        string => "0";

      # note that CONTEXT parameters are classes, not passed bundle parameters
      "vars[purge][type]"            string => "CONTEXT";
      "vars[purge][default]"         string => "0";

      "vars[process_match][type]"    string => "NON_EMPTY_STRING";

      "vars[start_command][type]"    string => "NON_EMPTY_STRING";

      "vars[packages][type]"         string => "LIST(NON_EMPTY_STRING)";

      "vars[server_packages][type]"  string => "LIST(NON_EMPTY_STRING)";

      # this is exposed via the API
      "returns[db]"    string => "$(db)";
      "returns[type]"  string => "$(type)";
      "returns[mode]"  string => "$(mode)";

  classes:
      "installing_mysql"      expression => strcmp("$(db)", "mysql");
      "installing_postgresql" expression => strcmp("$(db)", "postgresql");
      "installing_sqlite"     expression => strcmp("$(db)", "sqlite");

  vars:
      "type" string => "server",
      ifvarclass => "$(class_prefix)server";

      "type" string => "client",
      ifvarclass => "!$(class_prefix)server";

      "mode" string => "purging",
      ifvarclass => "$(class_prefix)purge";

      "mode" string => "installing",
      ifvarclass => "!$(class_prefix)purge";

      "all_packages" slist => { @(packages), @(server_packages) },
      ifvarclass => "$(class_prefix)server";

      "all_packages" slist => { @(packages) },
      ifvarclass => "!$(class_prefix)server";

  packages:

      "$(all_packages)"
      comment => "Install $(db) packages",
      handle => "db_install_packages_add",
      package_policy => "add",
      package_method => default:generic,
      classes => default:if_ok("$(class_prefix)ensure_database_running"),
      action => default:log_repaired("stdout","$(db) $(type) was installed"),
      ifvarclass => "!$(class_prefix)purge";

      "$(all_packages)"
      comment => "Remove $(db) packages.  SQLite packages should not be purged for Redhat-related distributions because rmp/yum rely on libsqlite.so.0",
      handle => "db_install_packages_remove",
      package_policy => "delete",
      package_method => default:generic,
      action => default:log_repaired("stdout","$(db) $(type) was removed"),
      ifvarclass => "$(class_prefix)purge.!(installing_sqlite.redhat)";

  files:

    redhat|centos|fedora::

      "/tmp/mysql.sock"
      comment => "Create a temp link to mysql.sock",
      handle => "db_mysql_files_mysql_sock_redhat_centos_fedora",
      ifvarclass => "installing_mysql.$(class_prefix)server",
      link_from => ln_s("/var/lib/mysql/mysql.sock");

    debian|ubuntu::

      "/tmp/mysql.sock"
      comment => "Create a temp link to mysql.sock",
      handle => "db_mysql_files_mysql_sock_debian_ubuntu",
      ifvarclass => "installing_mysql.$(class_prefix)server",
      link_from => ln_s("/var/run/mysqld/mysqld.sock");

  processes:

      "$(process_match)"
      comment => "Check for $(db) process",
      handle => "db_$(db)_processes_run",
      ifvarclass => "$(class_prefix)ensure_database_running.$(class_prefix)server",
      restart_class => "$(class_prefix)start_db";

      "$(process_match)"
      comment => "Teminate $(db) process",
      handle => "db_$(db)_processes_terminate",
      ifvarclass => "$(class_prefix)purge.$(class_prefix)server",
      signals => { "term","kill" };

  commands:

      "$(start_command)"
      comment => "Start/Restart command of $(db)",
      handle => "db_$(db)_commands_run",
      ifvarclass => "$(class_prefix)start_db";

  reports:
    verbose::
      "DB $(db) $(mode) in $(type) mode; packages $(all_packages)";
}
