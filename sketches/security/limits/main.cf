bundle agent meta_security_limits{
vars:
  "argument[domain][type][item]" string => "array";
  "optional_argument[mgmt_policy]" string => "string";
  "optional_argument[debug]" string => "string";

  # The default behavior is to only manage defined entries
  "default[mgmt_policy]" string => "present";

}

bundle agent security_limits(prefix){
# limits_[domain][type][item] string => "value";
# limits_mgmt_policy
# limits_debug
vars:
  "items" slist => { "core",
                     "data",
                     "fsize",
                     "memlock",
                     "nofile",
                     "rss",
                     "stack",
                     "cpu",
                     "nproc",
                     "as",
                     "maxlogins",
                     "maxsyslogins",
                     "priority",
                     "locks",
                     "sigpending",
                     "msqqueue",
                     "nice",
                     "rtprio",
                     "chroot",
                   };

  "types" slist => { "soft", "hard" };

  "domains" slist => getindices("$(prefix)");

classes:
  "$(domains)_$(types)_$(items)_defined" expression => isvariable("$(prefix)[$(domains)][$(types)][$(items)]");
  "completely_managed" expression => regcmp("(?i)only|complete|full|whole", "$($(prefix)mgmt_policy)");
  "partially_managed" expression => regcmp("(?i)present|exists|partial|defined|selective|exist", "$($(prefix)mgmt_policy)");
  "removal_managed" expression => regcmp("(?i)absent|delete|remove|removal|clear", "$($(prefix)mgmt_policy)");
  "debug_security_limits" expression => regcmp("(?i)true|yes|on", "$($(prefix)debug)");

  # This effectively defaults to partially managed
  "partially_managed" and => { "!completely_managed", "!removal_managed" };

files:
  debug_security_limits::
    "/tmp/limits.conf"
      handle => "security_limits_files_tmp_limits_conf_partially_managed",
      create => "true",
      edit_line => security_limits_set_limits_selective("$(prefix)", @(security_limits.items), @(security_limits.types), @(security_limits.domains)),
      classes => if_repaired("security_limits_repaired"),
      ifvarclass => "partially_managed";

    "/tmp/limits.conf"
      handle => "security_limits_files_tmp_limits_conf_completely_managed",
      create => "true",
      edit_defaults => empty,
      edit_line => security_limits_set_limits_complete("$(prefix)", @(security_limits.items), @(security_limits.types), @(security_limits.domains)),
      classes => if_repaired("security_limits_repaired"),
      ifvarclass => "completely_managed";

    "/tmp/limits.conf"
      handle => "security_limits_files_tmp_limits_conf_removal_managed",
      create => "true",
      edit_line => security_limits_set_limits_removal("$(prefix)", @(security_limits.items), @(security_limits.types), @(security_limits.domains)),
      classes => if_repaired("security_limits_repaired"),
      ifvarclass => "removal_managed";


  !debug_security_limits::
    "/etc/security/limits.conf"
      create => "true",
      handle => "security_limits_files_etc_security_limits_conf_partially_managed",
      edit_line => security_limits_set_limits_removal("$(prefix)", @(security_limits.items), @(security_limits.types), @(security_limits.domains)),
      perms => mog("644", "root", "root"),
      classes => if_repaired("security_limits_repaired"),
      ifvarclass => "partially_managed";

    "/etc/security/limits.conf"
      create => "true",
      handle => "security_limits_files_etc_security_limits_conf_completely_managed",
      edit_line => security_limits_set_limits_removal("$(prefix)", @(security_limits.items), @(security_limits.types), @(security_limits.domains)),
      edit_defaults => empty,
      perms => mog("644", "root", "root"),
      classes => if_repaired("security_limits_repaired"),
      ifvarclass => "completely_managed";

    "/etc/security/limits.conf"
      create => "true",
      handle => "security_limits_files_etc_security_limits_conf_removal_managed",
      edit_line => security_limits_set_limits_removal("$(prefix)", @(security_limits.items), @(security_limits.types), @(security_limits.domains)),
      perms => mog("644", "root", "root"),
      classes => if_repaired("security_limits_repaired"),
      ifvarclass => "removal_managed";



reports:
  debug_security_limits::
    "Security limits policy in debug mode";

  security_limits_repaired.debug_security_limits::
    "I repaired the /tmp/limits.conf file";

  security_limits_repaired.!debug_security_limits::
    "I repaired the /etc/security/limits.conf file";

}

bundle edit_line security_limits_set_limits_removal(prefix, items, types, domains){
vars:
  "myitems" slist => { @(items) };
  "mytypes" slist => { @(types) };
  "mydomains" slist => { @(domains) };

classes:
  "$(mydomains)_$(mytypes)_$(myitems)_defined" expression => isvariable("$(prefix)[$(mydomains)][$(mytypes)][$(myitems)]");

insert_lines:
  "# This file is managed by CFEngine, manual edits may be reverted";

delete_lines:
  "^\s*$(mydomains)\s+$(mytypes)\s+$(myitems)\s+.*"
    ifvarclass => canonify("$(mydomains)_$(mytypes)_$(myitems)_defined");
}

bundle edit_line security_limits_set_limits_selective(prefix, items, types, domains){
vars:
  "myitems" slist => { @(items) };
  "mytypes" slist => { @(types) };
  "mydomains" slist => { @(domains) };

classes:
  "$(mydomains)_$(mytypes)_$(myitems)_defined" expression => isvariable("$(prefix)[$(mydomains)][$(mytypes)][$(myitems)]");

insert_lines:
  "# This file is managed by CFEngine, manual edits may be reverted";
  "$(mydomains) $(mytypes) $(myitems) $($(prefix)[$(mydomains)][$(mytypes)][$(myitems)])"
    ifvarclass => and(canonify("$(mydomains)_$(mytypes)_$(myitems)_defined"), canonify("replace_done_$(edit.filename)_$(mydomains)_$(mytypes)_$(myitems)"));

replace_patterns:
  "^\s*($(mydomains)\s+$(mytypes)\s+$(myitems)\s+(?!$($(prefix)[$(mydomains)][$(mytypes)][$(myitems)])$)).*$"
    replace_with => value("$(mydomains) $(mytypes) $(myitems) $($(prefix)[$(mydomains)][$(mytypes)][$(myitems)])"),
    classes => always("replace_done_$(edit.filename)_$(mydomains)_$(mytypes)_$(myitems)"),
    ifvarclass => canonify("$(mydomains)_$(mytypes)_$(myitems)_defined");

}

bundle edit_line security_limits_set_limits_complete(prefix, items, types, domains){
vars:
  "myitems" slist => { @(items) };
  "mytypes" slist => { @(types) };
  "mydomains" slist => { @(domains) };

classes:
  "$(mydomains)_$(mytypes)_$(myitems)_defined" expression => isvariable("$(prefix)[$(mydomains)][$(mytypes)][$(myitems)]");

insert_lines:
  "# This file is managed by CFEngine, manual edits may be reverted";
  "$(mydomains) $(mytypes) $(myitems) $($(prefix)[$(mydomains)][$(mytypes)][$(myitems)])"
    ifvarclass => canonify("$(mydomains)_$(mytypes)_$(myitems)_defined");

}


