bundle agent tzconfig(timezone) {
# timezone should be a string representing the timezone 
# At least in the linux distros I checked this should be 
# relative to /usr/share/zoneinfo

    vars:
        linux::
            "zoneinfo" string => "/usr/share/zoneinfo";
    
    classes:
        "timezone_exists" expression => fileexists("$(zoneinfo)/$(timezone)");

    files:
        linux::
            "/etc/localtime"
                ifvarclass        => "timezone_exists",
                copy_from         => tzconfig_copy_zoneinfo("$(zoneinfo)/$(timezone)"),
                classes           => if_repaired("tzconfig_timezone_updated"),
                move_obstructions => "true",
                comment => "Copy the proper timezone file in place. We dont use
                            a symlink because that might cause problems across 
                            different filesystems, it has been a bug in the past.";
    reports:
        !timezone_exists::
            "Unable to set invalid timezone: $(timezone)";

        tzconfig_timezone_updated::
            "System timezone updated to $(timezone)";

}

body copy_from tzconfig_copy_zoneinfo(source){
    # Copy the file any symlinks reference this is superiour to symlinking
    # There have been bugs in the past that cause things to not function
    # properly if /etc/localtime is a symlink to a different filesystem
    source => "$(source)";
    copylink_patterns => { ".*", };
    compare => "hash";
}

